plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.kieronquinn.app.smartspacer.plugin.tasker'
    compileSdk compile_sdk

    defaultConfig {
        applicationId "com.kieronquinn.app.smartspacer.plugin.tasker"
        minSdk min_sdk
        targetSdk target_sdk
        versionCode 13
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs rootProject.ext.signingConfigs

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.outputFileName = new File(outputFileDir, output.outputFile.name)
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation project(path: ':shared')

    implementation('com.kieronquinn.smartspacer:sdk-client:1.0.0'){
        //Exclude transitive dependencies from the client as we already include them in :shared
        exclude group: "com.kieronquinn.smartspacer", module: "sdk-core"
        exclude group: "com.kieronquinn.smartspacer", module: "sdk-plugin"
    }

    //Tasker library
    implementation 'com.joaomgcd:taskerpluginlibrary:0.4.9'

    //Data storage
    ksp "androidx.room:room-compiler:$room_version"
    ksp 'com.github.bumptech.glide:ksp:4.15.1'

    //Markdown parsing
    implementation "io.noties.markwon:core:4.6.2"
    implementation "io.noties.markwon:ext-strikethrough:4.6.2"
    implementation "io.noties.markwon:html:4.6.2"

    //Built in icons
    implementation "com.mikepenz:iconics-core:5.4.0"
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:google-material-typeface:+@aar'
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:google-material-typeface-outlined:+@aar'
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:google-material-typeface-rounded:+@aar'
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:google-material-typeface-sharp:+@aar'
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:fontawesome-typeface:+@aar'
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:community-material-typeface:+@aar'
    //noinspection GradleDynamicVersion
    implementation 'com.mikepenz:phosphor-typeface:+@aar'

    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation 'junit:junit:4.13.2'
}